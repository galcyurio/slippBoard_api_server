buildscript {
    ext {
        versions = [
                kotlin       : '1.2.51',
                springBoot   : '2.0.4.RELEASE',
                mockitoKotlin: '2.0.0-RC2',
                asciidoctor  : '1.5.3',
        ]

        deps = [
                spring: [
                        jpa                : 'org.springframework.boot:spring-boot-starter-data-jpa',
                        security           : 'org.springframework.boot:spring-boot-starter-security',
                        web                : 'org.springframework.boot:spring-boot-starter-web',

                        devtools           : 'org.springframework.boot:spring-boot-devtools',
                        test               : 'org.springframework.boot:spring-boot-starter-test',
                        securityTest       : 'org.springframework.security:spring-security-test',

                        restdocsAsciidoctor: 'org.springframework.restdocs:spring-restdocs-asciidoctor',
                        restdocsMockmvc    : 'org.springframework.restdocs:spring-restdocs-mockmvc'
                ],
                kotlin: [
                        stdlib : 'org.jetbrains.kotlin:kotlin-stdlib-jdk8',
                        reflect: 'org.jetbrains.kotlin:kotlin-reflect',
                        jackson: 'com.fasterxml.jackson.module:jackson-module-kotlin',
                        mockito: "com.nhaarman.mockitokotlin2:mockito-kotlin:$versions.mockitoKotlin",
                ],
                h2db  : 'com.h2database:h2',
        ]
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$versions.springBoot"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$versions.kotlin"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$versions.kotlin"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:$versions.asciidoctor"
    }
}

subprojects {
    group 'net.slipp'
    version '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        jcenter()
    }
}

configure([
        project('repository'),
        subprojects.find { it.name.contains('server') },
]) {
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'kotlin-jpa'
    apply plugin: 'eclipse'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$versions.springBoot"
        }
    }
}

configure(subprojects.find { it.name.contains('server') }) {
    apply plugin: 'org.springframework.boot'
}